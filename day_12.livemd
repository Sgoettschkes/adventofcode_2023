# Day 12

## Star 23

There's just one problem - many of the springs have fallen into disrepair, so they're not actually sure which springs would even be safe to use! Worse yet, their condition records of which springs are damaged (your puzzle input) are also damaged! You'll need to help them repair the damaged records.

In the giant field just outside, the springs are arranged into rows. For each row, the condition records show every spring and whether it is operational (.) or damaged (#). This is the part of the condition records that is itself damaged; for some springs, it is simply unknown (?) whether the spring is operational or damaged.

However, the engineer that produced the condition records also duplicated some of this information in a different format! After the list of springs for a given row, the size of each contiguous group of damaged springs is listed in the order those groups appear in the row. This list always accounts for every damaged spring, and each number is the entire size of its contiguous group (that is, groups are always separated by at least one operational spring: #### would always be 4, never 2,2).

So, condition records with no unknown spring conditions might look like this:

```
#.#.### 1,1,3
.#...#....###. 1,1,3
.#.###.#.###### 1,3,1,6
####.#...#... 4,1,1
#....######..#####. 1,6,5
.###.##....# 3,2,1
```

However, the condition records are partially damaged; some of the springs' conditions are actually unknown (?). For example:

```
???.### 1,1,3
.??..??...?##. 1,1,3
?#?#?#?#?#?#?#? 1,3,1,6
????.#...#... 4,1,1
????.######..#####. 1,6,5
?###???????? 3,2,1
```

Equipped with this information, it is your job to figure out how many different arrangements of operational and broken springs fit the given criteria in each row.

In the first line (???.### 1,1,3), there is exactly one way separate groups of one, one, and three broken springs (in that order) can appear in that row: the first three unknown springs must be broken, then operational, then broken (#.#), making the whole row #.#.###.

The second line is more interesting: .??..??...?##. 1,1,3 could be a total of four different arrangements. The last ? must always be broken (to satisfy the final contiguous group of three broken springs), and each ?? must hide exactly one of the two broken springs. (Neither ?? could be both broken springs or they would form a single contiguous group of two; if that were true, the numbers afterward would have been 2,3 instead.) Since each ?? can either be #. or .#, there are four possible arrangements of springs.

The last line is actually consistent with ten different arrangements! Because the first number is 3, the first and second ? must both be . (if either were #, the first number would have to be 4 or higher). However, the remaining run of unknown spring conditions have many different ways they could hold groups of two and one broken springs:

```
?###???????? 3,2,1
.###.##.#...
.###.##..#..
.###.##...#.
.###.##....#
.###..##.#..
.###..##..#.
.###..##...#
.###...##.#.
.###...##..#
.###....##.#
```

In this example, the number of possible arrangements for each row is:

* ???.### 1,1,3 - 1 arrangement
* .??..??...?##. 1,1,3 - 4 arrangements
* ?#?#?#?#?#?#?#? 1,3,1,6 - 1 arrangement
* ????.#...#... 4,1,1 - 1 arrangement
* ????.######..#####. 1,6,5 - 4 arrangements
* ?###???????? 3,2,1 - 10 arrangements

Adding all of the possible arrangement counts together produces a total of 21 arrangements.

For each row, count all of the different arrangements of operational and broken springs that meet the given criteria. What is the sum of those counts?

```elixir
if :ets.info(:springs) != :undefined do
  :ets.delete(:springs)
end

:ets.new(:springs, [:set, :protected, :named_table])

defmodule Day12Star23 do
  def record_matches?(springs, damaged_springs) do
    springs
    |> Enum.join()
    |> String.split(".", trim: true)
    |> Enum.map(&String.length/1)
    |> Kernel.==(damaged_springs)
  end

  def all_solutions([], []), do: 1
  def all_solutions([], _), do: 0

  def all_solutions(springs, []) do
    if Enum.member?(springs, "#") do
      0
    else
      1
    end
  end

  def all_solutions(springs, [ds_hd | _ds_tail]) when length(springs) < ds_hd, do: 0
  def all_solutions(["." | tail], damaged_springs), do: cache(tail, damaged_springs)

  def all_solutions(["?" | tail], damaged_springs) do
    cache(tail, damaged_springs) + cache(["#" | tail], damaged_springs)
  end

  def all_solutions(["#" | _tail] = springs, [ds_hd | ds_tail]) do
    has_functional_spring_in_middle = Enum.take(springs, ds_hd) |> Enum.member?(".")
    has_damaged_spring_in_end = Enum.at(springs, ds_hd) == "#"

    if !has_functional_spring_in_middle and !has_damaged_spring_in_end do
      cache(Enum.drop(springs, ds_hd + 1), ds_tail)
    else
      0
    end
  end

  def cache(springs, damaged_springs) do
    case :ets.lookup(:springs, {springs, damaged_springs}) do
      [] ->
        result = all_solutions(springs, damaged_springs)
        :ets.insert(:springs, {{springs, damaged_springs}, result})

        result

      [{_, result}] ->
        result
    end
  end
end

"input_day_12_1.txt"
|> File.read!()
|> String.split("\n", trim: true)
|> Enum.map(fn line ->
  [springs, damaged_springs] = String.split(line, " ", trim: true)

  damaged_springs =
    damaged_springs
    |> String.split(",", trim: true)
    |> Enum.map(fn str ->
      str
      |> Integer.parse()
      |> then(fn {int, _} -> int end)
    end)

  %{springs: String.split(springs, "", trim: true), damaged_springs: damaged_springs}
end)
|> Enum.map(fn %{springs: springs, damaged_springs: damaged_springs} ->
  Day12Star23.all_solutions(springs, damaged_springs)
end)
|> Enum.sum()
```

## Star 24

As you look out at the field of springs, you feel like there are way more springs than the condition records list. When you examine the records, you discover that they were actually folded up this whole time!

To unfold the records, on each row, replace the list of spring conditions with five copies of itself (separated by ?) and replace the list of contiguous groups of damaged springs with five copies of itself (separated by ,).

So, this row:

```
.# 1
```

Would become:

```
.#?.#?.#?.#?.# 1,1,1,1,1
```

The first line of the above example would become:

```
???.###????.###????.###????.###????.### 1,1,3,1,1,3,1,1,3,1,1,3,1,1,3
```

In the above example, after unfolding, the number of possible arrangements for some rows is now much larger:

* ???.### 1,1,3 - 1 arrangement
* .??..??...?##. 1,1,3 - 16384 arrangements
* ?#?#?#?#?#?#?#? 1,3,1,6 - 1 arrangement
* ????.#...#... 4,1,1 - 16 arrangements
* ????.######..#####. 1,6,5 - 2500 arrangements
* ?###???????? 3,2,1 - 506250 arrangements

After unfolding, adding all of the possible arrangement counts together produces 525152.

Unfold your condition records; what is the new sum of possible arrangement counts?

```elixir
"input_day_12_2.txt"
|> File.read!()
|> String.split("\n", trim: true)
|> Enum.map(fn line ->
  [springs, damaged_springs] = String.split(line, " ", trim: true)

  springs =
    springs
    |> String.split("", trim: true)
    |> List.duplicate(5)
    |> Enum.map(fn l -> l ++ ["?"] end)
    |> List.flatten()
    |> List.delete_at(-1)

  damaged_springs =
    damaged_springs
    |> String.split(",", trim: true)
    |> Enum.map(fn str ->
      str
      |> Integer.parse()
      |> then(fn {int, _} -> int end)
    end)
    |> List.duplicate(5)
    |> List.flatten()

  %{springs: springs, damaged_springs: damaged_springs}
end)
|> Enum.map(fn %{springs: springs, damaged_springs: damaged_springs} ->
  Day12Star23.all_solutions(springs, damaged_springs)
end)
|> Enum.sum()
```
